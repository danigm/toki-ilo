nasin li nimi toki_pona {
    o toki e pini
    o toki e "#############" e pini
    o toki e "# toki pona #" e pini
    o toki e "#############" e pini
    o toki e pini
}

nasin li nimi nanpa_nasa {
    o toki e "mi insa e nasa_nanpa nasin" e pini
    nimi n1 li 4
    o pana e n1
}

nasin li nimi nimi_mute_nanpa_nasa {
    o pana e "nanpa nasa"
}

nasin li nimi nanpa_en_nanpa
li kama jo e nanpa n1 e nanpa n2 {
    o pana e n1 + n2
}

nasin li nimi toki_e_ni
li kama jo e nimi nimi_ni {
    o toki e nimi_ni e pini
}

nasin li nimi Fiponaki li kama jo e nanpa n1 {
    nimi r1 li 0
    nimi r2 li 0
    nimi rx li 1
    (n1 <= 1) la {
        nimi rx li n1
    } ante {
        nimi r1 li o Fiponaki e (n1 - 1)
        nimi r2 li o Fiponaki e (n1 - 2)
        nimi rx li r1 + r2
    }
    o pana e rx
}

nasin li nimi open {
    o toki e "ma ale o, toki!" e pini

    # var declaration
    o toki e pini
    nimi n1 li 5
    o toki e "nanpa li " e n1 e pini

    # var assignement and basic operation
    o toki e pini
    nimi n1 li 2
    nimi wan li 1
    o toki e "tu en wan li seme e " e n1 + wan e pini

    # if statement
    o toki e pini
    (n1 = wan) la {
        o toki e "nanpa li seme e wan" e pini
    } ante {
        o toki e "nanpa li seme ala e wan" e pini
    }

    # Booleans
    o toki e pini
    nimi nimi_lon li lon
    nimi nimi_ala li lon ala

    (nimi_lon) la {
        o toki e "nimi_lon li lon" e pini
        (nimi_lon en nimi_ala) la {
            o toki e "nimi_lon en nimi_ala li lon" e pini
        } ante {
            o toki e "nimi_lon en nimi_ala li lon ala" e pini
        }
    }

    # loop statement
    o toki e pini
    o toki e "nanpa li lili luka la o sin" e pini
    nimi n1 li 0
    o sin e ni {
        o toki e " - sin la nanpa li " e n1 e pini
        nimi n1 li (n1 + 1)
        (n1 = 5) la {
            o toki e " - o pini" e pini
            o pini
        }
    }

    # deep loop statement
    o toki e pini
    nimi nanpa1 li 0
    nimi nanpa2 li 0
    o sin e ni {
        (nanpa1 = 3) la { o pini }
        nimi nanpa2 li 0
        o sin e ni {
            o toki e " - sin la nanpa mute li " e nanpa1 e " e " e nanpa2 e pini
            nimi nanpa2 li (nanpa2 + 1)
            (nanpa2 = 3) la { o pini }
        }
        nimi nanpa1 li (nanpa1 + 1)
    }

    # function call statement
    o toki_pona

    # Function call with return statement
    nimi n1 li o nanpa_nasa
    o toki e "nanpa nasa li " e n1 e pini
    nimi nimi_mute li o nimi_mute_nanpa_nasa
    o toki e "nanpa nasa li " e nimi_mute e pini

    # Function call with return statement and parameters

    nimi n1 li o nanpa_en_nanpa e 2 e 3
    o toki e "2 en 3 li sama e " e n1 e pini
    o toki_e_ni e "pona"
    o toki e pini

    # Recursive function call
    # TODO: Fix this
    nimi i li 0
    o sin e ni {
        (i = 10) la { o pini }
        nimi i li i + 1

        nimi f li o Fiponaki e i
        o toki e " Fiponaki " e i e ": " e f e pini
    }
}
